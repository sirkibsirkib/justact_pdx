$comment
  the DMI agent states the following,
  laying down the 'rules of the game' to be agreed.
  firstly, two other facts are needed but missing.
  these are 'hooks' for other statements to follow.

$say dmi
  error (meta excluded)
    if not meta included.
  error (non-meta said meta included)
    if Sayer says (meta included)
    and diff {Sayer meta}.
  error (Sayer shouldnt be assigning roles/classes)
    if Sayer says (_ has R _)
    and not diff {R class role}
    and diff {dmi Sayer}.
  error (non marketplace selling)
    if Market says (_ sold to _ for Price)
    and not dmi says (Market has class marketplace).
  error (pdx restrictions not defined)
    if not dmi says (pdx restrictions are defined).

$comment
  agree on the above as a foundation for justifications (at time 0).

$agree 0 0

$comment
  the DMI agent continues, making the following statement
  that restricts how PDX is allowed to punish marketplaces.

$say dmi
  pdx restrictions on marketplaces are defined.
  pdx can punish (Market sold Item to Consumer for Price)
    if Market says (Item sold to Consumer for Price)
    and not pdx says ((Market sold Item to Consumer for Price) is pre- lawful).
  pdx can punish (Market sold Item to Consumer for Price)
    if Market says (Item sold to Consumer for Price)
    and not pdx says ((Market sold Item to Consumer for Price) is post- lawful).
  error (pdx punished Market without dmi permission)
    if pdx says (punish (Market sold Item to Consumer for Price))
    and not dmi says (pdx can punish (Market sold Item to Consumer for Price)).
  error (non pdx punishing)
    if Sayer says (punish _)
    and diff {Sayer pdx}.

$comment
  By including the following statement's final rule in their justifications,
  users can (e.g., eagerly) prove that their sales are pre-lawful. But by
  putting that rule alongside some other constraints, the PDX is restricting the
  cases where that rule is applicable. For example, this statement is useless
  to prove pre-lawfulness of non-zero price sales by knowledge institutions.

$say pdx
  error (knowledge institution makin' cash)
    if Offerer says (offer _ at _ for Price)
    and dmi says (Offerer has class (knowledge institution))
    and diff {0 Price}.
  offered pair (X Y)
    if offer (X Y) at _ for _.
  error (non pair offered Item)
    if offer Item at _ for _
    and not pdx says (offered pair Item).
  error (Sayer cannot offer on behalf of Offerer)
    if Sayer says (offer (Offerer _) at _ for _)
    and diff {Sayer Offerer}.
  (Market sold Item to Consumer for Price) is pre- lawful
    if Market says (Item sold to Consumer for Price)
    and Offerer says (offer Item at Market for Min)
    and Requester says (request Item at Market for Max).

$comment
  The following statement is needed alongside the agreement before anything is
  justifiable. Intuitively, it establishes some fundamental abstractions. For
  example, it introduces (_ says _) as a way to connect authors to the
  inferences of their statements, while abstracting away the message in which
  the inferring rule is contained. These new abstractions are protected by some
  extra rules that prevent other agents from ruining the abstraction. For
  example, the third rule prevents agents from saying things on each others'
  behalf, e.g., to cause confusion WRT on whose authority facts are stated.

$say meta
  meta included.
  error
    if error _.
  Sayer says Fact
    if Fact within (Sayer _)
    and diff {Sayer meta}.
  error (non-meta said "says")
    if Sayer says (_ says _)
    and diff {Sayer meta}.
  error (non-meta said "within")
    if _ says (_ within _).

$comment
  Finally, the following statements together bring rise to a particular
  transaction. Observe that three agents are required. They are laid out in a 
  practical order, but this order is not enforced anywhere. For example, the
  requester is free to learn of the offer before the offer is formally stated.
  However, each piece of the puzzle is ultimately needed when it is time to act.

$say seller offer (seller cow) at market for 400.

$say buyer request (seller cow) at market for 250.

$say market (seller cow) sold to buyer for 310.

$comment
  The following action is NOT permitted, because it includes the agreement's
  assertion that agents selling must have the 'marketplace' DMI classification,
  but this classification is not evidenced!

$enact market 0 0 1 2 3 4 5 6

$comment
  The following demonstrates how the DMI consortium can effectively make highly
  specialised amendments to the agreement on the fly using the same statement
  mechanism that the other agents use. Here, DMI introduces a new statement that
  has a highly specialised consequence: DMI grants the "market" agent the
  marketplace classification on the condition that another (effectful) fact
  is asserted by the "market" agent.

$say dmi
  market has class marketplace
    if market says (i agree to the dmi marketplace terms).

$say market
  i agree to the dmi marketplace terms.

$enact market 0 0 1 2 3 4 5 6 7 8

$inspect